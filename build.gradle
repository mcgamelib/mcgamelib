buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'kr.entree.spigradle' version '2.2.3'
    id 'io.freefair.lombok' version '5.3.0'
    id 'java-library'
    id 'jacoco'
    id 'idea'
}

apply from: "$rootDir/gradle/jacoco.gradle"
apply from: "$rootDir/gradle/publish.gradle"

if (project.hasProperty("local_script")) {
    apply from: file(local_script + "/build.local.gradle")
}

sourceCompatibility = 11
targetCompatibility = 11

ext {
    mcVersion = project.property("mcVersion")
}

group project.property("group")

spigot {
    name = project.property("pluginName")
    authors = [project.property("author")]
    apiVersion = project.property("apiVersion")
    load = STARTUP
    //    depends = ['']
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}

archivesBaseName = project.property("pluginName")

repositories {
    mavenCentral()
    jcenter()
    spigot()
    maven { url = 'https://jitpack.io' }
    maven { url = "https://repo.aikar.co/content/groups/aikar/" }
}

dependencies {
    implementation spigot(mcVersion)

    // add dependencies here
    implementation 'co.aikar:acf-paper:0.5.0-SNAPSHOT'
    api group: 'net.silthus.config-mapper', name: 'bukkit', version: '1.2.0'

    implementation 'net.kyori:adventure-api:4.5.0'
    implementation 'net.kyori:adventure-text-minimessage:4.0.0-SNAPSHOT'
    implementation 'net.kyori:adventure-platform-bukkit:4.0.0-SNAPSHOT'
    implementation group: 'net.kyori', name: 'adventure-text-feature-pagination', version: '4.0.0-SNAPSHOT'
    implementation group: 'net.kyori', name: 'adventure-text-serializer-plain', version: '4.5.0'

    //Test dependencies
    testImplementation 'org.junit.jupiter:junit-jupiter:5.+'
    testImplementation "org.mockito:mockito-core:3.+"
    testImplementation 'org.assertj:assertj-core:3.+'
    testImplementation 'com.github.seeseemelk:MockBukkit-v1.16:0.20.0'
    testImplementation 'org.jmockit:jmockit:1.49'
}

shadowJar {
    classifier = ''
    dependencies {
        include(dependency('co.aikar:acf-paper:'))
        include(dependency('net.kyori::'))
        include(dependency('net.silthus.config-mapper::'))
    }
    relocate 'co.aikar.commands', "shadow.${pluginName}.acf"
    relocate 'co.aikar.locales', "shadow.${pluginName}.locales"
    relocate 'net.kyori', "shadow.${pluginName}.text"
    relocate 'net.silthus.configmapper', "net.silthus.mcgamelib.config"
}

tasks.build.dependsOn(shadowJar)
tasks.publish.dependsOn(shadowJar)
tasks.prepareSpigotPlugins.dependsOn(shadowJar)

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    ignoreFailures = false
}

tasks.withType(Javadoc) {
    failOnError false
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}

processResources {
    project.properties.put("version", this.version)
    expand project.properties
}

defaultTasks 'build'
